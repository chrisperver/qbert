;; This is example boot code for the absolute example.

;; this code is stored at page 0, offset 0, and will be executed when the 
;; cart is started
org &0000
jp start

;; 0,GREEN,RED,BLUE,

; SHADES OF YELLOW AND RED FOR LOADER

palette:     ; COLOUR HTML RGB
  defw &0000
  defw &0040
  defw &0010
  defw &0109
  defw &0161
  defw &0292
  defw &02a2
  defw &02b2
  defw &03c2
  defw &04c2
  defw &07d1
  defw &09e1
  defw &09e1
  defw &0cf0
  defw &0df0
  defw &0ef0
  defw &0000

crtc_data: defb &3f,&28,&2e,&8e,&26,&00,&19,&1c,&00,&07,&00,&00,&30,&00,&c0,&00
end_crtc_data:

;; sequence to unlock asic
sequence: defb &ff,&00,&ff,&77,&b3,&51,&a8,&d4,&62,&39,&9c,&46,&2b,&15,&8a,&cd,&ee

boot_game:
  ;; OO-IULMM
  ;; disable upper and lower rom
  ld bc,&7f00+%10001100
  out (c),c
 
  jp &0100;&C100 ;7a00		;; and execute game
end_boot_game:

start:
di					;; disable interrupts
im 1					;; set interrupt mode 1
ld bc,&f782			;; setup initial PPI port directions
out (c),c
ld bc,&f400			;; set initial PPI port A (AY)
out (c),c
ld bc,&f600			;; set initial PPI port C (AY direction)
out (c),c

ld bc,&7fc0			;; set initial RAM configuration
out (c),c

;; unlock ASIC so we can access ASIC registers
ld b,&bc
ld hl,sequence
ld e,17
seq:
ld a,(hl)
out (c),a
inc hl
dec e
jr nz,seq

;; set initial CRTC settings (screen dimensions etc)
ld hl,end_crtc_data
ld bc,&bc0f
crtc_loop:
out (c),c
dec hl
ld a,(hl)
inc b
out (c),a
dec b
dec c
jp p,crtc_loop


  ; MOVE SCREEN MEMORY TO 8000-BFFF
  ld hl,0        ;; get scroll offset
  ld a,h
  or &20                    ;; This defines the "base" of the screen in 16k units.
                            ;; &00 -> screen uses &0000-&3fff
                            ;; &10 -> screen uses &4000-&7fff
                            ;; &20 -> screen uses &8000-&bfff
                            ;; &30 -> screen uses &c000-&ffff
  ld h,a
  ld bc,&bc0c                ;; select CRTC register 12
  out (c),c
  inc b                    ;; B = &BD
  out (c),h                ;; write to CRTC register 12
  dec b
  inc c                    ;; BC = &BC0D
  out (c),c                ;; select CRTC register 13
  inc b
  out (c),l                ;; write to CRTC register 13 

;; select page screen is in.
ld bc,&df81
out (c),c
ld hl,&c000		;; copy from ROM
ld de,&8000		;; to RAM &8000
ld bc,&4000
ldir

; SET STACK POINTER
ld sp,&C100;8000
; SET INTERRUPT FUNCTION?
;ld a,&c9 ;(RET)
;ld hl,&0038
;ld (hl),a

  ld bc,&7fb8 ; PAGE IN PLUS REGISTERS
  out (c),c   ; ASIC RAMBANK AT 4000-7FF

  ld de,&6400
  ld hl,palette
  ld bc,34
  ldir

  ld bc,&7fa0 ; PAGE OUT PLUS REGISTERS
  out (c),c

;; COPY BANKS OF CODE TO RAM
ld bc,&df82
out (c),c
ld hl,&c000		        ;; copy from ROM
ld de,&0100;&C100		;; to RAM under ROM
ld bc,&3F00;&3E00     
ldir

;; COPY BANKS OF CODE TO RAM
ld bc,&df83
out (c),c
ld hl,&c000		        ;; copy from ROM
ld de,&C100;&0100		;; to RAM under ROM
ld bc,&3F00             ;; LEAVE 100 FOR STACK
ldir

;; COPY BANKS OF CODE TO RAM
ld bc,&df84
out (c),c
ld hl,&c000		        ;; copy from ROM
ld de,&4000	         	;; to RAM under ROM
ld bc,&4000             
ldir


;ld sp,&C100
;; COPY BOOT CODE TO RAM
;; BOOT CODE MUST NOT BE IN UPPER OR LOWER ROM AREAS
;; ALSO MUST NOT BE OVERWRITTEN BY ABOVE GAME CODE
ld bc,&df80
out (c),c
ld hl,boot_game
ld de,&7f00
ld bc,end_boot_game-boot_game
ldir

  ;ld bc,&7f00+128+4+8+0 ; SET MODE TO 0 (LAST DIGIT SETS MODE)
  ;out (c),c

;; JUMP TO BOOT FUNCTION
;ld bc,&df80 ; SET MEMORY BACK TO STANDARD
;out (c),c
jp &7f00

;end start
